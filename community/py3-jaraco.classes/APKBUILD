# Contributor: Bart Ribbers <bribbers@disroot.org>
# Maintainer: Bart Ribbers <bribbers@disroot.org>
pkgname=py3-jaraco.classes
pkgver=3.3.0
pkgrel=0
pkgdesc="Utility functions for Python class constructs"
url="https://github.com/jaraco/jaraco.classes"
arch="noarch"
license="MIT"
depends="python3 py3-more-itertools"
makedepends="
	py3-gpep517
	py3-setuptools
	py3-setuptools_scm
	py3-wheel
	"
checkdepends="py3-pytest"
subpackages="$pkgname-pyc"
source="https://pypi.python.org/packages/source/j/jaraco.classes/jaraco.classes-$pkgver.tar.gz"
builddir="$srcdir/jaraco.classes-$pkgver"

# backwards compatibility for old name
replaces="py3-jaraco-classes"
provides="py3-jaraco-classes=$pkgver-r$pkgrel"

build() {
	gpep517 build-wheel \
		--wheel-dir .dist \
		--output-fd 3 3>&1 >&2
}

check() {
	pytest
}

package() {
	python3 -m installer -d "$pkgdir" .dist/jaraco.classes-$pkgver-py3-none-any.whl
}

sha512sums="
8099c85c93ed0eff37b1f870e2577b20488dea7b10273c00a89b43c0ef759c17c9a835916eef0fd97b3944bc5cd50b32e1393ac7c80cf7c7bfd1ee0d639d16cf  jaraco.classes-3.3.0.tar.gz
"
