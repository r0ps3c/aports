# Contributor: Bart Ribbers <bribbers@disroot.org>
# Maintainer: Bart Ribbers <bribbers@disroot.org>
pkgname=py3-jaraco.functools
pkgver=3.5.2
pkgrel=2
pkgdesc="Functools like those found in stdlib"
url="https://github.com/jaraco/jaraco.functools"
arch="noarch"
license="MIT"
# py3-setuptools_scm is required to set version of python module
depends="
	py3-jaraco.classes
	py3-more-itertools
	python3
	"
makedepends="
	py3-gpep517
	py3-setuptools
	py3-setuptools_scm
	py3-wheel
	"
checkdepends="py3-pytest"
source="https://pypi.python.org/packages/source/j/jaraco.functools/jaraco.functools-$pkgver.tar.gz"
builddir="$srcdir/jaraco.functools-$pkgver"

# backwards compatibility for old name
replaces="py3-jaraco-functools"
provides="py3-jaraco-functools=$pkgver-r$pkgrel"

build() {
	gpep517 build-wheel \
		--wheel-dir dist \
		--output-fd 3 3>&1 >&2
}

check() {
	pytest
}

package() {
	python3 -m installer -d "$pkgdir" dist/jaraco.functools-$pkgver-py3-none-any.whl
}

sha512sums="
3f67d151f4f6553b3e560b0e6ab71cf726d6c8f36ef5e9f69b6325605547797e19a3f9eb1331cf332db498538953ccaeb75820528e6599fd7cf125abdc073e69  jaraco.functools-3.5.2.tar.gz
"
